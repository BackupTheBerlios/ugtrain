#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

PACKAGE=ugtrain
VERSION=0.2.0
EMAIL=sebastian.riemer@gmx.de

AC_INIT(ugtrain, 0.2.0, sebastian.riemer@gmx.de)

AC_CONFIG_MACRO_DIR(m4)
AC_CANONICAL_SYSTEM

AM_INIT_AUTOMAKE(ugtrain, 0.2.0)
AM_MAINTAINER_MODE

AC_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)

LIB_CURRENT=0
LIB_REVERSION=0
LIB_AGE=0

VERSION_INFO="$LIB_CURRENT:$LIB_REVERSION:$LIB_AGE"
AC_SUBST(VERSION_INFO)

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AM_PROG_CC_STDC

case $target in
  *i[[3456789]]86*)
    TMP_CC="$CC"
    CC="$CC -m64"
    AC_MSG_CHECKING([whether $CC works])
    AC_LINK_IFELSE([AC_LANG_PROGRAM()],AC_MSG_RESULT([yes]),
                   [AC_MSG_RESULT([no])
                    AC_MSG_ERROR([Cannot build a 64-bit program, you need to install 64-bit development libraries.])])
    CC="$TMP_CC"
    ;;
  *x86_64*)
    TMP_CC="$CC"
    CC="$CC -m32"
    AC_MSG_CHECKING([whether $CC works])
    AC_LINK_IFELSE([AC_LANG_PROGRAM()],AC_MSG_RESULT([yes]),
                   [AC_MSG_RESULT([no])
                    AC_MSG_ERROR([Cannot build a 32-bit program, you need to install 32-bit development libraries.])])
    CC="$TMP_CC"
    ;;
esac
AM_CONDITIONAL([LINUX], test "$target_os" = "linux-gnu")

# Checks for pkg-config.
AC_SUBST(DEPS_CFLAGS)
AC_SUBST(DEPS_LIBS)

# Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(stdlib.h string.h unistd.h)

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T

# Checks for library functions.
AC_FUNC_STAT
AC_CHECK_FUNCS(ptrace memcpy memcmp getenv setenv)

AC_CHECK_HEADER(windows.h)

AC_CONFIG_FILES([
	Makefile
	linuxhooking/Makefile
	tools/Makefile
	src/Makefile])
AC_OUTPUT
